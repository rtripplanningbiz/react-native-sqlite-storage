buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.4'
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 31)
    buildToolsVersion safeExtGet('buildToolsVersion', '31.0.0')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 21)
        targetSdkVersion safeExtGet('targetSdkVersion', 31)
        versionCode 1
        versionName "1.0"
    }
    
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        pickFirst 'lib/*/libsqlcipher.so'
    }
}

repositories {
    mavenCentral()
    google()
    maven {
        url "https://maven.google.com"
    }
}

configurations.all {
    resolutionStrategy {
        force 'net.zetetic:android-database-sqlcipher:4.5.3'
    }
}

dependencies {
    implementation "com.facebook.react:react-native:${safeExtGet('reactNativeVersion', '+')}"
    implementation('net.zetetic:android-database-sqlcipher:4.5.3') {
        exclude group: 'net.zetetic', module: 'sqlcipher-android'
    }
    implementation 'androidx.sqlite:sqlite:2.2.0'
    implementation 'androidx.sqlite:sqlite-framework:2.2.0'
    implementation 'androidx.core:core:1.7.0'
}
